### Sample queries for CityGML to RDF transformations ###

# Common prefixes
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX gml:  <http://www.opengis.net/gml#>
PREFIX gmlowl:  <http://www.opengis.net/ont/gml#>
PREFIX units: <http://www.opengis.net/def/uom/OGC/1.0/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX strdf: <http://strdf.di.uoa.gr/ontology#>
PREFIX xlink: <http://www.w3.org/1999/xlink#>
# CityGML 2.0
#PREFIX core: <http://www.opengis.net/citygml/2.0/core#>
#PREFIX bldg: <http://www.opengis.net/citygml/building/2.0/building#>
#PREFIX brid: <http://www.opengis.net/citygml/bridge/2.0/bridge#>
#PREFIX luse: <http://www.opengis.net/citygml/landuse/2.0/landuse#>
#PREFIX app:  <http://www.opengis.net/citygml/appearance/2.0/appearance#>
#PREFIX dem:  <http://www.opengis.net/citygml/relief/2.0/relief#>
#PREFIX frn:  <http://www.opengis.net/citygml/cityfurniture/2.0/cityfurniture#>
#PREFIX gen:  <http://www.opengis.net/citygml/generics/2.0/generics#>
#PREFIX grp:  <http://www.opengis.net/citygml/cityobjectgroup/2.0/cityobjectgroup#>
#PREFIX tex:  <http://www.opengis.net/citygml/texturedsurface/2.0/texturedsurface#>
#PREFIX tun:  <http://www.opengis.net/citygml/tunnel/2.0/tunnel#>
#PREFIX veg:  <http://www.opengis.net/citygml/vegetation/2.0/vegetation#>
#PREFIX wtr:  <http://www.opengis.net/citygml/waterbody/2.0/waterbody#>
# CityGML 3.0
PREFIX core: <http://www.opengis.net/citygml/3.0/core#>
PREFIX bldg: <http://www.opengis.net/citygml/building/3.0/building#>
PREFIX brid: <http://www.opengis.net/citygml/bridge/3.0/bridge#>
PREFIX luse: <http://www.opengis.net/citygml/landuse/3.0/landuse#>
PREFIX app:  <http://www.opengis.net/citygml/appearance/3.0/appearance#>
PREFIX dem:  <http://www.opengis.net/citygml/relief/3.0/relief#>
PREFIX frn:  <http://www.opengis.net/citygml/cityfurniture/3.0/cityfurniture#>
PREFIX gen:  <http://www.opengis.net/citygml/generics/3.0/generics#>
PREFIX grp:  <http://www.opengis.net/citygml/cityobjectgroup/3.0/cityobjectgroup#>
PREFIX tex:  <http://www.opengis.net/citygml/texturedsurface/3.0/texturedsurface#>
PREFIX tun:  <http://www.opengis.net/citygml/tunnel/3.0/tunnel#>
PREFIX veg:  <http://www.opengis.net/citygml/vegetation/3.0/vegetation#>
PREFIX vers:  <http://www.opengis.net/citygml/versioning/3.0/versioning#>
PREFIX wtr:  <http://www.opengis.net/citygml/waterbody/3.0/waterbody#>
# Dataset prefixes
PREFIX data: <https://github.com/VCityTeam/UD-Graph/citygml3_versioning#>
#PREFIX data: <https://github.com/VCityTeam/UD-Graph/LYON_1ER_BATI_2015-20_bldg-patched#>

#1 return all buildings

#SELECT DISTINCT ?building
#WHERE {
#    ?building a bldg:Building .
#}


#2 return a building and it's linked individuals

#SELECT DISTINCT ?object ?type
#WHERE {
#    data:BU_69381AT70 (!<>)* ?object .
#
#    ?object a owl:NamedIndividual ;
#        a ?type .
#
#    FILTER (STR(?type) != STR(owl:NamedIndividual))
#}


#3 return building geometry

#SELECT ?geometry ?type ?serialization
#WHERE {
#    data:BU_69381AT70 (!<>)* ?object .
#
#    ?object a owl:NamedIndividual ;
#        a ?type ;
#        geo:hasGeometry ?geometry .
#
#    ?geometry geo:asGML ?serialization .
#
#    FILTER (STR(?type) != STR(owl:NamedIndividual))
#}


#4 return all GeoSPARQL features within 10 m of a point 

#SELECT DISTINCT ?feature ?type
#WHERE {
#    ?feature geo:hasGeometry ?geometry ;
#        rdf:type ?type .
#
#    ?geometry geo:asGML ?serialization .
#
#    BIND ("<gml:Point xmlns:gml=\"http://www.opengis.net/ont/gml\" srsName=\"EPSG:3946\"><gml:pos>1842653.24996800 5175722.37503700</gml:pos></gml:Point>"^^geo:gmlLiteral
#        as ?point)
#
#    FILTER (geof:distance(?point, ?serialization, units:metre ) < 10) .
#}


#5 return all geometry within a given bounding box

#SELECT DISTINCT ?geometry ?type
#WHERE {
#
#    ?geometry geo:asGML ?serialization ;
#        a ?type .
#
#    BIND("<gml:LinearRing xmlns:gml='http://www.opengis.net/ont/gml' srsName='EPSG:3946'><gml:posList>1842640 5175720 1842640 5175750 1842660 5175750 1842660 5175720 1842640 5175720</gml:posList></gml:LinearRing>"^^geo:gmlLiteral
#        as ?bbox)
#
#    FILTER (geof:sfIntersects(?serialization, ?bbox)) .
#    FILTER(STR(?type) != STR(owl:NamedIndividual)) .
#}


#6 return all cityobjects within a given bounding box

#SELECT DISTINCT ?cityobjectmember ?type
#WHERE {

#    ?geometry geo:asGML ?serialization .

#    BIND("<gml:LinearRing xmlns:gml='http://www.opengis.net/ont/gml' srsName='EPSG:3946'><gml:posList>1842640 5175720 1842640 5175750 1842660 5175750 1842660 5175720 1842640 5175720</gml:posList></gml:LinearRing>"^^geo:gmlLiteral
#        as ?bbox) .

#    FILTER (geof:sfIntersects(?serialization, ?bbox)) .

#    data:CityModel_0 core:CityModel.cityObjectMember ?cityobjectmember .

#    ?cityobjectmember a ?type ;
#        (!<>)* ?geometry .
#}


#7 return all cityobjects within a version

#SELECT DISTINCT ?cityobjectmember ?type
#WHERE {

#    data:LYON_1ER_snapshot-2000_01_01 a vers:Version ;
#        vers:Version.versionMember ?cityobjectmember .

#    ?cityobjectmember a ?type .
#}


#8 return all buildings that intersect a point in time

SELECT DISTINCT ?building ?tp1 ?tp2
WHERE {
    BIND("2010-01-01T00:00:00"^^xsd:dateTime as ?point)
    ?building a bldg:Building ;
        core:AbstractFeatureWithLifespan.creationDate ?tp1 ;
        core:AbstractFeatureWithLifespan.terminationDate ?tp2 .
    FILTER(?tp1 < ?point && ?point < ?tp2)
}


#9 return all buildings that intersect a point in time (with stSPARQL)

#SELECT DISTINCT ?building ?existence ?target
#WHERE {
#    ?building a bldg:Building ;
#        core:AbstractFeatureWithLifespan.creationDate ?tp1 ;
#        core:AbstractFeatureWithLifespan.terminationDate ?tp2 .
#    BIND(strdf:period(?tp1, ?tp2) as ?existence)
#    BIND("[2011-01-01T00:00:00, 'UC')"^^strdf:period as ?target)
#    FILTER(strdf:during(?target, ?existence))
#}

#LIMIT 100